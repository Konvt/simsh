# This file is licensed under the MIT License.
# Please see the LICENSE file in the root of the repository for the full license text.
# Copyright (c) 2024 Konvt

<statement> ::= <nonempty_statement>
              | '\n'
              | EOF

<nonempty_statement> ::= <expression> <statement_extension>
                       | <output_redirection> <statement_extension>
                       | '(' <inner_statement> <statement_extension>
                       | '!' <logical_not> <statement_extension>

<statement_extension> ::= <connector> <nonempty_statement>
                        | <redirection> <statement_extension>
                        | '\n'
                        | EOF

<inner_statement> ::= <expression> <inner_statement_extension>
                    | <output_redirection> <inner_statement_extension>
                    | '!' <logical_not> <inner_statement_extension>
                    | '(' <inner_statement> <inner_statement_extension>

<inner_statement_extension> ::= <connector> <inner_statement>
                              | <redirection> <inner_statement_extension>
                              | ';' <inner_statement>
                              | ';' ')'
                              | ')'

<logical_not> ::= <expression>
                | '(' <inner_statement>

<connector> ::= '&&'
              | '||'
              | '|'
              | ';'

<redirection> ::= <output_redirection>
                | <input_redirection>

<output_redirection> ::= &>{1,2} <expression> (<output_redirection_extension>)?
                       | (\d*)>{1,2} <expression> (<output_redirection_extension>)?
                       | (\d*)>&(\d*) (<combined_redirection_extension>)?

<output_redirection_extension> ::= (\d*)>&(\d*)

<combined_redirection_extension> ::= &>{1,2} <expression>
                                   | (\d*)>{1,2} <expression>

<input_redirection> ::= < <expression>

<expression> ::= [^&|!<>"':\(\)\^%#\s]+
               | " [^"\n]* "
